/UNITS,SI
!**** TITLE & CONSTANTS *****************************************************
/title, Crack in coated layer
!**** PARAMETERS **************************************************************
!constants
WIDTH=3
FORCE=10e6
TimeStep=5e-5
TimeForce=5e-5
TimeTicks=1500e-5
Damping=1e-5

! comment out following lines in batch mode
!LHEIGHT=0.01
!HEIGHT=0.8
!CCENTER=0.1
!AAA=0.09
!ACTUATOR=0.9
!SENSOR_OFFSET=0.1

ex1=70e9
nu1=.34
dens1=2700

ex2=210e9
nu2=.28
dens2=7700

! Variables
DEPTH=CCENTER - AAA
SIZE=2*AAA
A=AAA
TIPX=SIZE/100
TIPY=SIZE/10
SENSOR=ACTUATOR+SENSOR_OFFSET
CENTER=WIDTH/2
RANGE=WIDTH/4

!**** MATERIAL PROPERTIES *****************************************************
/prep7 

ET,1,PLANE183
ET,2,PLANE183

MPTEMP,1,1
MPDATA,EX,1,,21e9   
MPDATA,PRXY,1,,nu1
mp,dens,1,dens1  

MPTEMP,2,2
MPDATA,EX,2,,ex1
MPDATA,PRXY,2,,nu2
mp,dens,2,dens2  

MP,DAMP,1,Damping
MP,DAMP,2,Damping

!*** GEOMETRY *****************************************************************
!keypoints
k,1,-CENTER, LHEIGHT
k,2,-CENTER+ACTUATOR, LHEIGHT
k,3,-CENTER+SENSOR, LHEIGHT
k,4,CENTER, LHEIGHT
k,5,CENTER, 0
k,6, A, 0
k,7,-A, 0
k,8,-CENTER, 0

k,11,-CENTER, -HEIGHT
k,12,-A, -HEIGHT
k,13,A, -HEIGHT
k,14,CENTER, -HEIGHT

k,21,-A, -DEPTH
k,22,0, -DEPTH
k,23,A, -DEPTH
k,24,A, -DEPTH-SIZE
k,25,0, -DEPTH-SIZE
k,26,-A, -DEPTH-SIZE

k,31,-TIPX, -DEPTH-TIPY
k,32,TIPX, -DEPTH-TIPY
k,33,TIPX, -DEPTH-SIZE+TIPY
k,34,-TIPX, -DEPTH-SIZE+TIPY

!lines
lstr,1,2
lstr,2,3
lstr,3,4
lstr,4,5
lstr,5,6
lstr,6,7
lstr,7,8
lstr,8,1

lstr,8,11
lstr,11,12
lstr,12,13
lstr,13,14
lstr,14,5

lstr,21,22
lstr,22,23
lstr,23,24
lstr,24,25
lstr,25,26
lstr,26,21

lstr,7,21
lstr,6,23
lstr,24,13
lstr,26,12

lstr,31,22
lstr,22,32
lstr,32,33
lstr,33,25
lstr,25,34
lstr,34,31


AL,1,2,3,4,5,6,7,8
AL,20,19,23,10,9,7
AL,12,13,5,21,16,22
AL,18,17,22,11,23
AL,6,21,15,14,20
AL,26,27,17,16,15,25
AL,14,24,29,28,18,19


!*** LOADS ******************************************************************** 
lsel,s,,,4
lsel,a,,,8
lsel,a,,,9
lsel,a,,,13
!SFL,all,PRES,-FORCE
lsel,all

DL,10,,ALL,0
DL,11,,ALL,0
DL,12,,ALL,0

!*** MESHING *****************************************************************                
MSHAPE,0,2D

asel,all
aatt,1
asel,s,area,,1
aatt,2
asel,all

AMESH,ALL

!***SOLUTION***************************************************************************************************               
/SOLU           
ANTYPE,4

TRNOPT,REDUC
M,ALL,ALL

DELTIM,TimeStep

!step by step problem definition
TIME,0.000
FK,2,FY,-FORCE*10
LSWRITE,1

TIME,TimeForce
FKDELE,2,ALL
LSWRITE,2

TIME,TimeTicks
LSWRITE,3

LSSOLVE,1,3,1
FINISH

!***POSTPROCESSING***************************************************************************************************               
/POST26
FILE,'file','rdsp','.'  

KSEL,S,,,2
NSLK,S
NSOL,2,ndnext(0),U,Y,S1
STORE,MERGE

KSEL,S,,,3
NSLK,S
NSOL,3,ndnext(0),U,Y,S2
STORE,MERGE

!***EXPORTING********************************************************************************************************               
*GET,size,VARI,,NSETS 

*dim,A1,array,size
*dim,A2,array,size
*dim,A3,array,size

VGET,A1(1),1
VGET,A2(1),2
VGET,A3(1),3

*VOPER,A2(1),A2(1),MULT,1e3 
*VOPER,A3(1),A3(1),MULT,1e3

*CFOPEN,data,dat
*VWRITE,A1(1),A2(1),A3(1)        ! Write array in given format to file ".dat"
(f12.6,f20.8,f20.8)
*CFCLOSE
